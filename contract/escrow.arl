archetype escrow

/* Roles */
variable seller       : role = @tz1MZrh8CvYkp7BfLQMcm6mg5FvL5HRZfACw
variable buyer        : role = @tz1MZrh8CvYkp7BfLQMcm6mg5FvL5HRZfACw
variable taxcollector : role = @tz1MZrh8CvYkp7BfLQMcm6mg5FvL5HRZfACw

/* price */
variable price     : tez = 0tz

/* rates */
constant taxrate      : rational = 20%
constant securityrate : rational = 110%

(* escrow deadline *)
constant deadline : date = now + 1d

(* states *)
states =
 | Created initial
 | Aborted
 | Funded
 | InTransit
 | Completed

transition abortCreated () {
  called by buyer
  from Created
  to Aborted
}

transition abortFunded () {
  called by buyer
  from Funded
  to Aborted
}

transition abort () {
  from any
  to Aborted when { now > deadline }
}

transition fund () {
  called by buyer
  from Created
  to Funded when { transferred >= (100% + taxrate + securityrate) * price }
}

transition complete () {
  called by buyer
  from Funded
  to Completed
  with effect {
    transfer price                  to seller;
    transfer (taxrate * price)      to taxcollector;
    transfer (securityrate * price) to buyer;
  }
}
